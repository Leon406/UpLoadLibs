apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

//上传库是api 需要改为compile
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':ucrop')
    compile 'com.android.support:appcompat-v7:26.+'
    compile 'com.android.support:recyclerview-v7:26.+'
    compile 'com.github.bumptech.glide:glide:4.3.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.7'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'me.leon406:rx_activity_result:1.0.6'
}
/** 以下开始是将Android Library上传到jcenter的相关配置**/

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// 版本号，下次更新是只需要更改版本号即可
version = "1.2.3"
//项目主页
def siteUrl = 'https://github.com/Leon406'    // project homepage
def gitUrl = 'https://github.com/Leon406' // project git到组织名称名字，必须填写
group = groupId;

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    // options.links "https://github.com/linglongxin24/FastDev/tree/master/mylibrary/docs/javadoc"
    failOnError false
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到jcenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'pictureselector For Android'  //根据实际情况修改
                //artifactId = 'imagepicker'//项目名称
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer { //填写的一些基本信息
                        id 'leon'
                        name 'leonlambett'
                        email 'deadogone@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到jcenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "pictureselector"    //发布到JCenter上的项目名字，必须填写
        desc = 'This is pictureselector'    //项目描述
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}


//Pom手动配置
//<dependencies>
//<dependency>
//<groupId>com.android.support</groupId>
//      <artifactId>appcompat-v7</artifactId>
//<version>26.+</version>
//      <scope>compile</scope>
//</dependency>
//    <dependency>
//      <groupId>io.reactivex.rxjava2</groupId>
//<artifactId>rxjava</artifactId>
//      <version>2.1.7</version>
//<scope>compile</scope>
//    </dependency>
//<dependency>
//<groupId>io.reactivex.rxjava2</groupId>
//      <artifactId>rxandroid</artifactId>
//<version>2.0.1</version>
//      <scope>compile</scope>
//</dependency>
//	<dependency>
//      <groupId>com.github.bumptech.glide</groupId>
//<artifactId>glide</artifactId>
//      <version>4.3.1</version>
//<scope>compile</scope>
//    </dependency>
//<dependency>
//<groupId>me.leon406</groupId>
//      <artifactId>rx_activity_result</artifactId>
//<version>1.0.6</version>
//      <scope>compile</scope>
//</dependency>
//  </dependencies>